/**
 * Pretty-printing Aspect Declarations
 *
 * @author Martin Bravenboer <martin.bravenboer@gmail.com>
 */
module aspectj/pp/aspect/Declaration
imports
  aspectj/pp/aspect/DeclareDeclaration
  aspectj/pp/aspect/AdviceDeclaration
  aspectj/pp/aspect/IntertypeMemberDeclaration

rules

  aspectj-to-box :
    AspectDec(b1, b2) -> V vs=0 [b1 b2]

  aspectj-to-box :
    AspectDecHead(mods, b2, opt3, opt4, opt5, opt6) -> H hs=1 [~*mods KW["aspect"] b2 ~*b3 ~*b4 ~*b5 ~*b6]
    where
        <option-to-boxes> opt3 => b3
      ; <option-to-boxes> opt4 => b4
      ; <option-to-boxes> opt5 => b5
      ; <option-to-boxes> opt6 => b6      

  aspectj-to-box :
    PerTarget(b) -> H hs=0 [KW["pertarget"] "(" b ")"]

  aspectj-to-box :
    PerThis(b) -> H hs=0 [KW["perthis"] "(" b ")"]

  aspectj-to-box :
    PerCFlow(b) -> H hs=0 [KW["percflow"] "(" b ")"]

  aspectj-to-box :
    PerCFlowBelow(b) -> H hs=0 [KW["percflowbelow"] "(" b ")"]

  aspectj-to-box :
    PerTypeWithin(b) -> H hs=0 [KW["pertypewithin"] "(" b ")"]

  aspectj-to-box :
    IsSingleton() -> box |[ KW["issingleton"] ]|

  aspectj-to-box :
    AspectBody(b*) -> V [V vs=0 is=2 [KW["{"] V vs=1 [b*]] KW["}"]]

  aspectj-to-box :
    Privileged() -> box |[ KW["privileged"] ]|
