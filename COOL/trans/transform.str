module transform
imports
  libstratego-lib
  libjava-front
  include/cool
  libstratego-gpp
  libstratego-sglr
  aspectj/pretty-print

strategies
  transform =
        path := <id>;
        ast := <parse-file(|<import-term(cool.tbl)>)>path;
        annotated_ast := <topdown(\trm -> trm{<try(gen-source-location)>}\)>ast;
        transformed_ast := <transform-ast>annotated_ast;
        <pp-aspectj-string>transformed_ast

  transform-ast =
        <convert><id>

  gen-source-location:
	trm -> [<origin-location>trm, <origin-file>trm]

  origin-location = prim("SSL_EXT_origin_location", <id>)
  origin-file = prim("SSL_EXT_origin_file", <id>)

  gen-isRunByOthers-method = 
     !MethodDec(
        MethodDecHead([MarkerAnno(Id("HideMethod")), Private(), Static()], None(), Boolean(), Id("isRunByOthers"), [Param([], TypeName(Id("List")), Id("methState"))], None()),
		Block(
            [Return(
	          Some(
	            LazyAnd(
	              Not(
	                Invoke(
	                  Method(MethodName(AmbName(Id("methState")), Id("isEmpty")))
	                , []
	                )
	              )
	            , Not(
	                Invoke(
	                  Method(MethodName(AmbName(Id("methState")), Id("contains")))
	                , [Invoke(
	                     Method(MethodName(AmbName(Id("Thread")), Id("currentThread")))
	                   , []
	                   )]
	                )
	              )
	            )
	          )
	        )]
        )
      )

  gen-method-state(|method2index) =
     ?MethodSignature(Id(name), _);
     methodsig := <id>;
     !FieldDec( [MarkerAnno(Id("HideField")), Private()], 
     		ClassOrInterfaceType(TypeName(Id("List")), None()), 
		[VarDec(Id(<gen-method-state-name(|method2index)>methodsig),
		       Invoke(Method(MethodName(AmbName(Id("Collections")),Id("synchronizedList"))), [NewInstance(None(), ClassOrInterfaceType(TypeName(Id("Vector")), None()), [], None())]))] )

  gen-method-state-name(|method2index) =
     ?MethodSignature(Id(name), _);
     methodsig := <strip-annos><id>;
     <concat-strings>[name, <int-to-string><hashtable-get(|methodsig)>method2index, "State"]

  gen-requires-method-name(|method2index) =
     ?MethodSignature(Id(name), _);
     methodsig := <strip-annos><id>;
     <concat-strings>["requires_", name, <int-to-string><hashtable-get(|methodsig)>method2index]

  gen-on-entry-method-name(|method2index) =
     ?MethodSignature(Id(name), _);
     methodsig := <strip-annos><id>;
     <concat-strings>["on_entry_", name, <int-to-string><hashtable-get(|methodsig)>method2index]

  gen-on-exit-method-name(|method2index) =
     ?MethodSignature(Id(name), _);
     methodsig := <strip-annos><id>;
     <concat-strings>["on_exit_", name, <int-to-string><hashtable-get(|methodsig)>method2index]

  make-while-statement(|condition, whileStatements*) =
     !While(condition, Block(whileStatements*))

  make-while-condition(|literals*) =
     switch <length>literals*
     	case equal(|0) : !False()
     	case equal(|1) : <\ [x] -> x \>literals*
     	otherwise      : [prefix | rest*] := literals*;
					   	 !LazyOr(<make-while-condition(|rest*)>, prefix)
     end
     
  gen-coordinator-head(|coordinator_type) =
  	name := <fetch_class_name>coordinator_type;
    fullTypeName := <fetch_full_class_name>coordinator_type;
    annos := [MarkerAnno(Id("HideType")), Public(), Privileged()];
    perThis := Some(
          PerThis(
               Exec(
              ConstrPattern(
                AnnoPattern([])
              , ModPattern([])
              , ConstrName(RefTypePattern(NamePattern(fullTypeName)))
              , [FormalWildcard()]
              , None()
              )
            )
          )
        );
    !AspectDecHead(annos, Id(<concat-strings>[name, "Coord"]), None(), None(), None(), perThis)

  make-requires-method(|coordinator_type, requires_map, requires_methods, method2index) =
     ?MethodSignature(Id(name), types);
     methodsig := <id>;
     newmethodname := <gen-requires-method-name(|method2index)>methodsig;
     <hashtable-put(|<strip-annos>methodsig, newmethodname)>requires_methods;
     !IntertypeMethodDec(
            IntertypeMethodDecHead(
              [MarkerAnno(Id("HideMethod")), Public()]
            , None()
            , Boolean()
            , coordinator_type
            , None()
            , Id(newmethodname)
            , []
            , None()
            )
          , Block([Return(Some(<hashtable-get(|methodsig)>requires_map))])
          )

  make-on-entry-method(|coordinator_type, on_entry_map, on_entry_methods, method2index) =
    ?MethodSignature(Id(name), types);
    methodsig := <id>;
    newmethodname := <gen-on-entry-method-name(|method2index)>methodsig;
    <hashtable-put(|<strip-annos>methodsig, newmethodname)>on_entry_methods;
    !IntertypeMethodDec(
            IntertypeMethodDecHead(
              [MarkerAnno(Id("HideMethod")), Public()]
            , None()
            , Void()
            , coordinator_type
            , None()
            , Id(newmethodname)
            , []
            , None()
            )
          , <hashtable-get(|methodsig)>on_entry_map
          )

  make-on-exit-method(|coordinator_type, on_exit_map, on_exit_methods, method2index) =
    ?MethodSignature(Id(name), types);
    methodsig := <id>;
    newmethodname := <gen-on-exit-method-name(|method2index)>methodsig;
    <hashtable-put(|<strip-annos>methodsig, newmethodname)>on_exit_methods;
    !IntertypeMethodDec(
            IntertypeMethodDecHead(
              [MarkerAnno(Id("HideMethod")), Public()]
            , None()
            , Void()
            , coordinator_type
            , None()
            , Id(newmethodname)
            , []
            , None()
            )
          , <hashtable-get(|methodsig)>on_exit_map
          )

  convert =
  	?CompilationUnit(package, imports, types);
  	compilation_unit := <id>;
  	if coordinator := <get-coordinator(|types)>compilation_unit
  	then
  		coordinator_type := <fetch-coordinator-type>coordinator; 
  		converted_coordinator := <convert-coordinator-declaration(|coordinator_type)>coordinator;
    	other_types := <remove-all(?CoordinatorDec(_, _))>types;
    	!CompilationUnit(package, // the package stays the same
    					 <generate-imports(|imports, coordinator_type)>,
    					 <conc>([converted_coordinator], other_types))
  	else
  		<id>compilation_unit // or maybe raise an error?
  	end
  	
  generate-imports(|original_imports, coordinator_type) =
  	<conc>(original_imports,
  		   [TypeImportOnDemandDec(PackageName([Id("java"),Id("util")])),
  		    TypeImportOnDemandDec(PackageName([Id("org"),Id("aspectj"),Id("lang"),Id("annotation")]))])

  get-coordinator(|types) =
  	<getfirst(?CoordinatorDec(_, _))>types  	

rules

  fetch-coordinator-type:
  	CoordinatorDec(header, body) -> <\ CoordinatorDecHead(type) -> type \>header

  convert-coordinator-declaration(|coordinator_type):
    CoordinatorDec(header, body) -> AspectDec(header', body')
    with
		header' := <gen-coordinator-head(|coordinator_type)>;
		body' := <gen-coordinator-body(|coordinator_type)>body

  gen-coordinator-body(|coordinator_type):
    CoordinatorBody(declarations*) -> AspectBody(
    				   <conc> (
				   	  <add-condition-fields(|coordinator_type)>condition,
					  <add-coordinator-fields(|coordinator_type)> declarations*,
					  <map(gen-method-state(|method2index))>methods,
					  [<gen-isRunByOthers-method>],
					  <map(make-requires-method(|coordinator_type, requires_map, requires_methods, method2index))><hashtable-keys>requires_map, 
					  <map(make-on-entry-method(|coordinator_type, on_entry_map, on_entry_methods, method2index))><hashtable-keys>on_entry_map,
					  <map(make-on-exit-method(|coordinator_type, on_exit_map, on_exit_methods, method2index))><hashtable-keys>on_exit_map,
					  <concat><map(make-lock-and-unlock-methods(|coordinator_type, on_exit_methods, requires_methods, on_entry_methods, selfex_methods, mutex_methods,
					   method2index, method2selfex_sig, method2mutex_sig, method2additions_sig))>methods
					  ))
    with
      requires_map   := <new-hashtable>;
      on_exit_map    := <new-hashtable>;
      on_entry_map   := <new-hashtable>;

      requires_methods := <new-hashtable>;
      on_exit_methods  := <new-hashtable>;
      on_entry_methods := <new-hashtable>;
      method2index     := <new-hashtable>;

      method2additions_sig := <new-hashtable>;
      method2selfex_sig    := <new-hashtable>;
      method2mutex_sig     := <new-hashtable>;

      condition      := <getfirst(?ConditionDec(_))>declarations*;
      selfex_methods' := <get-methods-list><getfirst(?Selfex(_))>declarations*;
      mutex_methods'  := <get-methods-list><getfirst(?Mutex(_))>declarations*;
      <map(\ methodsig -> <hashtable-put(|<strip-annos>methodsig, methodsig)>method2selfex_sig \ )>selfex_methods';
      <map(\ methodsig -> <hashtable-put(|<strip-annos>methodsig, methodsig)>method2mutex_sig \ )>mutex_methods';
      selfex_methods := <map(\ methodsig -> <strip-annos>methodsig \)>selfex_methods';
      mutex_methods  := <map(\ methodsig -> <strip-annos>methodsig \)>mutex_methods';
      methods	     := <union>(selfex_methods, mutex_methods);
      <map(\ methodsig -> <hashtable-put(|methodsig, <length><hashtable-keys>method2index)>method2index \ )>methods;
      method_additions := <retain-all(?MethodAdditions(_,_,_,_))>declarations*;
      <map(\ MethodAdditions(methodsig, _, _, _) -> <hashtable-put(|methodsig, methodsig)>method2additions_sig \ )>method_additions;
      <map(fillMethodMaps(|requires_map,on_exit_map,on_entry_map, method2index))>method_additions

   make-lock-and-unlock-methods(|coordinator_type, on_exit_methods, requires_methods, on_entry_methods, selfex_methods, mutex_methods, method2index, method2selfex_sig, method2mutex_sig, method2additions_sig):
     method -> [
     	       	<make-lock-method(|coordinator_type, requires_methods, on_entry_methods, selfex_methods, mutex_methods, method2index, method2selfex_sig, method2mutex_sig, method2additions_sig)>methodsig,
    	       	<make-unlock-method(|coordinator_type, on_exit_methods, method2index)>methodsig
	           ]
	 with
	 	methodsig := <strip-annos><id>

   make-unlock-method(|coordinator_type, on_exit_methods, method2index):
     MethodSignature(Id(name), types) -> MethodDec(MethodDecHead(annos, None(), Void(), Id(<concat-strings>["unlock_", name]), [Param([], coordinator_type, Id("obj"))], None()), Block(statements*))
     with
		methodsig := <id>;
		pointcut := <concat-strings>["execution(* ", name ,"(..)) && this(obj)"];
        annos := [
           MarkerAnno(Id("HideMethod")),
           SingleElemAnno(TypeName(Id("After")), Lit(String([Chars(pointcut)]))),
           Public(),
           Synchronized()];
        if <elem>(methodsig ,<hashtable-keys>on_exit_methods)
        then
          callOnExitMethod := <\ name -> ExprStm(Invoke(Method(MethodName(AmbName(Id("obj")), Id(name))), [])) \><hashtable-get(|methodsig)>on_exit_methods
        else
          callOnExitMethod := None()
		end;
       	statements* := <conc> (
       	   [ExprStm(Invoke(Method(MethodName(AmbName(Id(<gen-method-state-name(|method2index)>methodsig)), Id("remove"))), [Invoke(Method(MethodName(AmbName(Id("Thread")), Id("currentThread"))), [])]))],
		   [callOnExitMethod],
		   [ExprStm(Invoke(Method(MethodName(Id("notifyAll"))), []))])

   make-lock-method(|coordinator_type, requires_methods, on_entry_methods, selfex_methods, mutex_methods, method2index, method2selfex_sig, method2mutex_sig, method2additions_sig):
     MethodSignature(Id(name), types) -> MethodDec(MethodDecHead(annos, None(), Void(), Id(<concat-strings>["lock_", name]), [Param([], coordinator_type, Id("obj"))], None()), Block(statements*))
     with
       methodsig := <strip-annos><id>;
       if <elem>(methodsig, mutex_methods)
       then
         mutexMethods* := <remove-all(equal(|methodsig))>mutex_methods;
         mutex_location := [<gen-source-location-ann><hashtable-get(|methodsig)>method2mutex_sig]
       else
         mutexMethods* := [];
         mutex_location := []
       end;
       if <elem>(methodsig, selfex_methods)
       then
         exclusiveMethods* := <conc> ([methodsig], mutexMethods*);
         exclusivelocations := <conc> ([<gen-source-location-ann><hashtable-get(|methodsig)>method2selfex_sig], mutex_location)
       else
         exclusiveMethods* := mutexMethods*;
         exclusivelocations := mutex_location
       end;
       literalsExceptRequirement* := <map(\ methodsig -> Invoke(Method(MethodName(Id("isRunByOthers"))),[ExprName(Id(<gen-method-state-name(|method2index)>methodsig))]) \)>exclusiveMethods*;
       if <elem>(methodsig, <hashtable-keys>requires_methods)
       then
         requirement := <\ expr -> Not(expr) \><\ name -> Invoke(Method(MethodName(AmbName(Id("obj")), Id(name))),[]) \><hashtable-get(|methodsig)>requires_methods;
	     literals* := [requirement | literalsExceptRequirement*]
       else
         literals* := literalsExceptRequirement*
       end;
       whileCondition := <make-while-condition(|<reverse>literals*)>;
       if <elem>(methodsig, <hashtable-keys>on_entry_methods)
       then 
         callOnEntryMethod := <\ name -> ExprStm(Invoke(Method(MethodName(AmbName(Id("obj")), Id(name))),[])) \><hashtable-get(|methodsig)>on_entry_methods
       else
         callOnEntryMethod := None() 
       end;
       if <elem>(methodsig, <hashtable-keys>method2additions_sig)
       then
       	 source_location := <conc> ([<gen-source-location-ann><hashtable-get(|methodsig)>method2additions_sig], exclusivelocations)
       else
       	 source_location := exclusivelocations
       end;
       statements* := <conc> ([<make-while-statement(|whileCondition, 
			                                          [Try(Block([ExprStm(Invoke(Method(MethodName(Id("wait"))),[]))]),[Catch(Param([],ClassOrInterfaceType(TypeName(Id("InterruptedException")),None()),Id("e")),Block([Throw(NewInstance(None(), ClassOrInterfaceType(TypeName(Id("RuntimeException")), None()), [], None()))]))])])>],
       		       	       [callOnEntryMethod],
			       [ExprStm(Invoke(Method(MethodName(AmbName(Id(<gen-method-state-name(|method2index)>methodsig)),Id("add"))),[Invoke(Method(MethodName(AmbName(Id("Thread")),Id("currentThread"))),[])]))]);
	   pointcut := <concat-strings>["execution(* ", name ,"(..)) && this(obj)"];
       annos :=
         	[MarkerAnno(Id("HideMethod")),
         	 <gen-source-location-ann>coordinator_type,
          	 SingleElemAnno(TypeName(Id("Before")), Lit(String([Chars(pointcut)]))),
          	 Public(),
             Synchronized()]

   fillMethodMaps(|requires_map,on_exit_map,on_entry_map, method2index):
     MethodAdditions(methodsig, requires, on_entry, on_exit) -> None()
     with
      if not(<equal>(requires, None()))
      then
	<hashtable-put(|methodsig, <\ Some(Requires(expr)) -> expr \>requires)>requires_map
      end;
      if not(<equal>(on_entry, None()))
      then
	<hashtable-put(|methodsig, <\ Some(OnEntry(block)) -> block \>on_entry)>on_entry_map
      end;
      if not(<equal>(on_exit, None()))
      then
	<hashtable-put(|methodsig, <\ Some(OnExit(block)) -> block \>on_exit) >on_exit_map  
      end

   add-condition-fields(|coordinator_type):
     ConditionDec(assignments*) -> declarations*
     with
     	declarations* := <map(\ Assign(ExprName(name),expr) ->
     	 IntertypeFieldDec([MarkerAnno(Id("HideField")), Private()], Boolean(), coordinator_type, None(), VarDec(name, expr)) \)>assignments*

   add-coordinator-fields(|coordinator_type):
     declarations* -> <map(\ FieldDec(anno*, type, var_decs*) ->
      <add-coordinator-fields2(|<topdown(strip-annos)>coordinator_type, <topdown(strip-annos)>type, anno*)>var_decs* \)>declarations'*
     with
	 	declarations'* := <topdown(strip-annos)><retain-all(?FieldDec(_,_,_))>declarations*

   add-coordinator-fields2(|coordinator_type, type, anno*) =
    <map(\x -> IntertypeFieldDec(<conc>([MarkerAnno(Id("HideField")), Private()], anno*), type, coordinator_type, None(), x) \)><id>

   get-methods-list:
     Selfex(methodsignatures*) -> methodsignatures*

   get-methods-list:
     Mutex(methodsignatures*) -> methodsignatures*

   get-methods-list:
     None() -> []

   gen-source-location-ann:
               x -> Anno(TypeName(Id("BridgedSourceLocation")),[
                 ElemValPair(Id("file"),Lit(String([Chars(<get-file>x)]))),
                 ElemValPair(Id("line"),Lit(Deci(<int-to-string><inc><get-line><get-location>x))),
                 ElemValPair(Id("module"),Lit(String([Chars("BoundedStack.cool")])))])

   get-location: t{[a|b]} -> a
   get-file: t{[a|[b]]} -> b
   get-line = \ (x, _, _, _) -> x \

   fetch_class_name: TypeName(_, Id(name)) -> name
   fetch_class_name: TypeName(Id(name)) -> name

   fetch_full_class_name: TypeName(packageOrTypeName, Id(name)) -> <concat-strings>[<fetch_full_class_name>packageOrTypeName, ".", name]
   fetch_full_class_name: TypeName(Id(name)) -> name
   fetch_full_class_name: PackageOrTypeName(Id(name)) ->name
   fetch_full_class_name: PackageOrTypeName(packageOrTypeName, Id(name)) -> <concat-strings>[<fetch_full_class_name>packageOrTypeName, ".", name]
