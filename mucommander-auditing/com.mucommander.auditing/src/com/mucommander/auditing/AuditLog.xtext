grammar com.mucommander.auditing.AuditLog with org.eclipse.xtext.common.Terminals

generate auditLog "http://www.mucommander.com/auditing/AuditLog"

import "http://www.eclipse.org/xtext/common/JavaVMTypes" as types

Model: (commands+=Command)*;
        
Command:
	'logs for' type=[types::JvmDeclaredType|QualifiedName] (overrides?='(overrides)')? ':'
        (
        	cases+=Case(',' cases+=Case)*
        	(',' 'otherwise' 'log' default=[types::JvmEnumerationLiteral])?
        )?
    ';'
;

Case:
	'case' state=State
	       ('&' (fields+=[types::JvmField]))*
	       'log' msg=[types::JvmEnumerationLiteral] (vars+=[types::JvmField])*
;

enum State: start|finish|interrupt|pause|resume;

QualifiedName: ID ("." ID)*;
