/*
 * generated by Xtext
 */
package org.ovirt.engine.permissions.generator

import org.eclipse.emf.ecore.resource.Resource
import org.eclipse.xtext.generator.IFileSystemAccess
import org.eclipse.xtext.generator.IGenerator
import org.ovirt.engine.permissions.permissions.Command
import org.ovirt.engine.permissions.permissions.Condition
import org.ovirt.engine.permissions.permissions.Permission

/**
 * Generates code from your model files on save.
 * 
 * See https://www.eclipse.org/Xtext/documentation/303_runtime_concepts.html#code-generation
 */
class PermissionsGenerator implements IGenerator {
	
	override void doGenerate(Resource resource, IFileSystemAccess fsa) {
		fsa.generateFile('org/ovirt/engine/core/bll/Permissions.aj',
			'''
				package org.ovirt.engine.core.bll;
				
				import java.util.*;
				import org.ovirt.engine.core.bll.utils.PermissionSubject;
				import org.ovirt.engine.core.common.VdcObjectType;
				import org.ovirt.engine.core.common.businessentities.ActionGroup;

				import org.slf4j.Logger;
				import org.slf4j.LoggerFactory;

				public privileged aspect Permissions {
					protected Logger log = LoggerFactory.getLogger(getClass());
					«FOR command:resource.allContents.filter(typeof(Command)).toIterable»
						«command.compile»
					«ENDFOR»
				}
			'''
		)
	}

	def compile(Command command) '''
		List<PermissionSubject> around(«command.type.qualifiedName» command): execution(* getPermissionCheckSubjects()) && this(command) {
			List<PermissionSubject> permissions = new ArrayList<>();
			«IF command.extends!=null»
			    «IF command.extends.cond!=null»if («IF command.extends.cond.not»!«ENDIF»command.«command.extends.cond.operation.simpleName»())«ENDIF»
			    permissions.addAll(proceed(command));
			«ENDIF»

			«FOR permission:command.permissions»
				«permission.compile»
			«ENDFOR»

			return permissions;
		}

	'''

	def compile(Permission permission) '''
	try {
		«IF permission.conditional»
			if («FOR condition:permission.conditions SEPARATOR ' && ' »«condition.compile»«ENDFOR») {
		«ENDIF»	    
				permissions.add(new PermissionSubject(command.«permission.objectId.simpleName»(),
					VdcObjectType.«permission.objectType.simpleName»,
					ActionGroup.«permission.actionGroup.simpleName»));
		«IF permission.conditional»
			}
		«ENDIF»
	} catch (Exception e) {
		log.error("Could not add permission subject for: VdcObjectType.«permission.objectType.simpleName» ActionGroup.«permission.actionGroup.simpleName»");
	}

	'''

    def compile(Condition condition) '''«IF condition.not»!«ENDIF»command.«condition.operation.simpleName»()'''
}
